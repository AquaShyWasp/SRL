{$DEFINE SRL_LOBBYSCREEN_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL/osr.simba}
{$ENDIF}

type
  TRSLobbyScreen = type TRSInterface;
  
procedure TRSLobbyScreen.Setup; override;
begin
  inherited;

  Self.Name := 'Login Screen';
end;

procedure TRSLobbyScreen.Setup(Mode: ERSClientMode); overload;
begin
  Self.Mode := Mode;

  Self.Alignment.Left := [@RootInterface.X1];
  Self.Alignment.Right := [@RootInterface.X2];
  Self.Alignment.Top := [@RootInterface.Y1];
  Self.Alignment.Bottom := [@RootInterface.Y1, 502];
  Self.Alignment.Center := [764, 0];
end;  

function TRSLobbyScreen.ClickPlayButton(Click: Boolean = True): Boolean;
var
  B: TBox;
begin
  if FindTextColor('CLICK HERE TO PLAY', RS_FONT_BOLD_12, $FFFFFF, 0, Self.Bounds, B) = 1 then
  begin
    if Click then
      Mouse.Click(B, MOUSE_LEFT);
      
    Result := True;
  end;
end;
  
function TRSLobbyScreen.IsOpen: Boolean; overload;
begin
  Result := Self.ClickPlayButton(False);
end;

function TRSLobbyScreen.IsOpen(WaitTime: Int32; Interval: Int32 = -1): Boolean; overload;
begin
  if Interval = -1 then
    Interval := SRL.TruncatedGauss(50, 1500);

  Result := WaitUntil(Self.IsOpen(), Interval, WaitTime);
end;

procedure TRSLobbyScreen.Debug(Bitmap: TMufasaBitmap); override;
begin
  if not Self.IsOpen() then
    Exit;

  inherited;
end;

function TRSLobbyScreen.EnterGame: Boolean;
begin
  Result := Self.ClickPlayButton() and RSClient.IsLoggedIn(5000);
end;

var
  LobbyScreen: TRSLobbyScreen;

procedure TRSClient.ClientModeChanged; override;
begin
  inherited;

  LobbyScreen.Setup(Self.Mode);
end;

procedure TSRL.Setup; override;
begin
  inherited;

  LobbyScreen.Setup();
end;

procedure TSRL.Debug(Bitmap: TMufasaBitmap); override;
begin
  inherited;

  LobbyScreen.Debug(Bitmap);
end;
