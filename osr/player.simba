{$DEFINE SRL_PLAYER_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL/osr.simba}
{$ENDIF}

type
  PRSPlayer = ^TRSPlayer;

  TRSPlayer = record
    LoginName: String;
    Password: String;
    Pin: String;
    Member: Boolean;
    Worlds: TIntegerArray;

    CurrentWorld: Int32;

    // User Data
    Data: Pointer;
    Variants: TVariantArray;
  end;

  TRSPlayerArray = array of TRSPlayer;

var
  Players: TRSPlayerArray;

var
  CurrentPlayer: Int32;

procedure TRSPlayerArray.Add(LoginName: String; Password: String; Pin: String = ''; Member: Boolean = True; Worlds: TIntegerArray = []);
var
  Player: TRSPlayer;
begin
  Player.LoginName := LoginName;
  Player.Password := Password;
  Player.Pin := Pin;
  Player.Member := Member;
  Player.Worlds := Worlds;

  SetLength(Player.Variants, 100);

  Self := Self + Player;
end;

procedure TRSPlayer.LoggedIn;
begin
  { override me to know when a player was logged in }
end;

function TRSPlayer.Login: Boolean;
begin
  if RSClient.IsLoggedIn() then
    Exit(True);

  if LoginScreen.IsOpen() then
  begin
    if Self.Worlds <> [] then
    begin
      Self.CurrentWorld := Self.Worlds[Random(Length(Self.Worlds))];
      if (LoginScreen.GetCurrentWorld() <> Self.CurrentWorld) and (not LoginScreen.SwitchToWorld(Self.CurrentWorld)) then
        Exit(False);
    end;

    Result := LoginScreen.Login(Self.LoginName, Self.Password);
  end else
    Result := LobbyScreen.EnterGame();

  if Result then
    Self.LoggedIn();
end;
