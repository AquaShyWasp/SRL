{$DEFINE SRL_PINSCREEN_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL/osr.simba}
{$ENDIF}

type
  TRSPinScreen = type TRSInterface;

procedure TRSPinScreen.Setup;
begin
  Self.Name := 'Pin Screen';
  Self.Mode := RSClient.Mode;

  case Self.Mode of
    RS_CLIENT_FIXED:
      begin
        Self.Alignment.Left := [@InterfaceArea.X1];
        Self.Alignment.Right := [@InterfaceArea.X2];
        Self.Alignment.Top := [@InterfaceArea.Y1, 11];
        Self.Alignment.Bottom := [@InterfaceArea.Y2];
        Self.Alignment.Center.MaxWidth := 488;
        Self.Alignment.Center.MaxHeight := 305;
      end;

    RS_CLIENT_RESIZABLE_CLASSIC, RS_CLIENT_RESIZABLE_BOTTOM:
      begin
        Self.Alignment.Left := [@InterfaceArea.X1];
        Self.Alignment.Right := [@InterfaceArea.X2];
        Self.Alignment.Top := [@InterfaceArea.Y1, 10];
        Self.Alignment.Bottom := [@InterfaceArea.Y2];
        Self.Alignment.Center.MaxWidth := 488;
        Self.Alignment.Center.MaxHeight := 305;
      end;
  end;
end;

function TRSPinScreen.GetDigitBoxes: TBoxArray;
begin
  Result := Grid(4, 3, 64, 64, [30, 8], [Self.X1 + 21, Self.Y1 + 83]);
  Result[3].X1 -= 10;
  Result[3].X2 -= 10;

  Delete(Result, 7, 1);
  Delete(Result, 10, 1);
end;

function TRSPinScreen.ClickDigit(Digit: Int32): Boolean; overload;
var
  Digits: TIntegerArray;
  Boxes: TBoxArray;
  I, J: Int32;
begin
  Boxes := Self.GetDigitBoxes();

  SetLength(Digits, Length(Boxes));
  for I := 0 to High(Digits) do
    Digits[I] := -1;

  for I := 0 to High(Boxes) do
    for J := 0 to High(Boxes) do
      if FindTextColor(ToString(J), RS_FONT_BOLD_12, 32767, 0, Boxes[I], 1) then
        Digits[I] := J;

  if (Length(Digits.FindAll(-1)) <= 1) and (Digits.UniqueCount() = Length(Digits)) then
  begin
    I := Digits.Find(Digit);
    if (I = -1) then
      I := Digits.Find(-1);

    Result := I <> -1;
    if Result then
      Mouse.Click(Boxes[I], MOUSE_LEFT);
  end;
end;

function TRSPinScreen.ValidatePin(Pin: String): Boolean;
begin
  Result := (Length(Pin) = 4) and Pin.IsDigit();
end;

function TRSPinScreen.Enter(Pin: String): Boolean;
var
  I: Int32;
  Digit: Int32;
begin
  for 1 to 3 do // 3 Attempts
    for I := 1 to 4 do
    begin
      if FindTextColor(['FIRST', 'SECOND', 'THIRD', 'FOURTH'][I-1], RS_FONT_BOLD_12, $FFFFFF, 0, Self.Bounds, 1) then
      begin
        Digit := StrToInt(Pin[I]);
        if not WaitUntil(Self.ClickDigit(Digit), SRL.TruncatedGauss(500, 2000), SRL.TruncatedGauss(4000, 6000)) then
          Break;
      end;

      if (I = 4) then
      begin
        Result := BankScreen.IsOpen(3000);

        Exit;
      end;
    end;
end;

function TRSPinScreen.IsOpen: Boolean;
begin
  Result := FindTextColor('Please enter your PIN', RS_FONT_BOLD_12, $1F98FF , 0, Self.Bounds, 1);
end;

function TRSPinScreen.IsOpen(WaitTime: Int32; Interval: Int32 = -1): Boolean; overload;
begin
  if (Interval = -1) then
    Interval := SRL.TruncatedGauss(0, 1500);

  Result := WaitUntil(Self.IsOpen(), Interval, WaitTime);
end;

procedure TRSPinScreen.Debug(Bitmap: TMufasaBitmap); override;
begin
  if (not Self.IsOpen()) then
    Exit;

  inherited(Bitmap);

  Bitmap.DrawBoxes(GetDigitBoxes(), False, $00FF00);
end;

var
  PinScreen: TRSPinScreen;

function TRSBankScreen.IsOpen(WaitForItems: Boolean = True): Boolean; override;
var
  Pin: String;
begin
  if PinScreen.IsOpen() then
  begin
    if (Length(Players) = 0) then
      Self.Fatal('Cannot enter bank pin: No players are declared!');

    Pin := Players[CurrentPlayer].Pin;
    if not PinScreen.ValidatePin(Pin) then
      Self.Fatal('Invalid bank pin: ' + Pin);

    if not PinScreen.Enter(Pin) then
      Self.Fatal('Failed to enter bank pin');
  end;

  Result := inherited();
end;

procedure TSRL.Setup; override;
begin
  PinScreen.Setup();

  inherited;
end;

procedure TSRL.Debug(Bitmap: TMufasaBitmap); override;
begin
  inherited;

  PinScreen.Debug(Bitmap);
end;
