{$DEFINE SRL_STATS_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL/osr.simba}
{$ENDIF}

type
  ERSEquipmentButton = (
    HELM,
    CAPE,
    AMULET,
    AMMO,
    WEAPON,
    BODY,
    SHIELD,
    LEGS,
    GLOVES,
    FEET,
    RING,
    
    STATS,
    PRICES,
    DEATH,
    FOLLOWER
  );
  
  TRSEquipment = type TRSInterface;

procedure TRSEquipment.Setup; override;
begin
  inherited;

  Self.Name := 'Equipment';
end;

procedure TRSEquipment.Setup(Mode: ERSClientMode); overload;
begin
  Self.Mode := Mode;

  Self.Alignment.Left := [@GameTab.X1];
  Self.Alignment.Right := [@GameTab.X2];
  Self.Alignment.Top := [@GameTab.Y1];
  Self.Alignment.Bottom := [@GameTab.Y2];
end;

function TRSEquipment.IsOpen: Boolean;
begin
  Result := (GameTabs.GetCurrentTab() = ERSGameTab.EQUIPMENT);
end;

function TRSEquipment.Open: Boolean;
begin
  Result := GameTabs.Open(ERSGameTab.EQUIPMENT);
end;

function TRSEquipment.GetButtons: TRSButtonArray;
begin
  Result := Self.FindButtons([[34,34], [40,40]]);
end;

function TRSEquipment.GetButton(Button: ERSEquipmentButton): TRSButton;
var
  Buttons: TRSButtonArray := Self.GetButtons();
begin
  if Length(Buttons) = Length(ERSEquipmentButton) then
    Result := Buttons[Button];
end;

function TRSEquipment.FindItem(Identifer: String; out Bounds: TBox): Boolean;
var
  Boxes: TBoxArray;
  Slots: TIntegerArray;
begin
  if not Self.Open() then
    Exit;
    
  Boxes := Self.GetButtons().ToBoxes();
  
  Slots := ItemFinder.Find(Identifer, Boxes);
  if Length(Slots) > 0 then
  begin
    Bounds := Boxes[Slots[0]];//}// CC BUG!
    
    Result := True;
  end;
end;

function TRSEquipment.ClickItem(Identifer: String; Option: String = ''): Boolean;
var
  B: TBox;
begin
  if Self.FindItem(Identifer, B) then
  begin
    Mouse.Move(B);

    if Option <> '' then
      Result := ChooseOption.Select(Option)
    else
    begin
      Result := True;

      Mouse.Click(MOUSE_LEFT);
    end;
  end;
end;

function TRSEquipment.IsEquipped(Identifer: String): Boolean;
var
  B: TBox;
begin
  Result := Self.FindItem(Identifer, B);
end;

var
  Equipment: TRSEquipment;

procedure TRSClient.ClientModeChanged; override;
begin
  inherited;

  Equipment.Setup(Self.Mode);
end;

procedure TSRL.Setup; override;
begin
  inherited;

  Equipment.Setup();
end;

procedure TSRL.Debug(Bitmap: TMufasaBitmap); override;
begin
  inherited;

  Equipment.Debug(Bitmap);
end;
